# Returns the string character at the given position.
string.charat = function(s, n) {
  return string.sub(s, n, n+1)
}

# Returns the index of the first occurrence of the given string m
# within another string s. If none is found, this function returns
# nil.
string.indexoffirst = function(s, m) {
  var ls = string.length(s)
  var lm = string.length(m)
  var i = 0
  while(i < ls-lm+1) {
    if(string.sub(s, i, i+lm) == m) return i
    i = i + 1
  }
  return nil
}

# Returns the index of the last occurrence of the given string m
# within another string s. If none is found, this function returns
# nil.
string.indexoflast = function(s, m) {
  var ls = string.length(s)
  var lm = string.length(m)
  var i = ls - lm + 1
  while(i >= 0) {
    if(string.sub(s, i, i+lm) == m) return i
    i = i - 1
  }
  return nil
}

# Splits a string s using the delimiters in d. The string list is
# returned in a table indexed by value (starting at 0).
string.split = function(s, d) {
  var i1 = 0  # index to move along s (token start)
  var i2 = 0  # index to move along s (token end)
  var c = 0   # token count
  var t = {}  # token list
  var ls = string.length(s)
  var ld = string.length(d)
  # Go through string s
  while(i2 < ls) {
    # Try every delimiter
    var j = 0   # index to move along d
    var f = nil # whether the delimiter was found or not
    while(j < ld and (not f)) {
      if(string.charat(s, i2) == string.charat(d, j)) {
        # Delimiter found
        f = 1
        # Is it worth adding a new token?
        if(i2 > i1) {
          t[c] = string.sub(s, i1, i2)
          c = c + 1
        }
        # Start new token
        i1 = i2 + 1
      }
      else {
        # Next delimiter
        j = j + 1
      }
    }
    # Next string character
    i2 = i2 + 1
  }
  # Is it worth adding a new token?
  if(i2 > i1) {
    t[c] = string.sub(s, i1, i2)
  }
  # Return token list
  return t;
}

x = "le osterie, di fuori porta"
i = 0
while(i < string.length(x)) {
  var y = string.sub(x, i)
  print("'", y, "' ", string.length(y), " characters")
  i = i + 1
}

print(string.charat(x, 0))
print(string.charat(x, 5))
print(string.charat(x, 10))
print(string.charat(x, 15))
print(string.charat(x, 20))
print(string.charat(x, 25))

print(string.indexoffirst(x, "porta"))
print(string.indexoffirst(x, "pizza"))

print(string.indexoflast(x, "osterie"))
print(string.indexoflast(x, "pizza"))

z = string.split(x, " ,")
print(size(z), " tokens found")
foreach(z, function(k, v) {
    print("<", k, ",", v, ">")
  })

x = "   ,,,   , ,"
z = string.split(x, " ,")
print(size(z), " tokens found")
foreach(z, function(k, v) {
    print("<", k, ",", v, ">")
  })

print("'", string.concat("ciao", " ", "peppe", "!"), "'")
